LIBNAME epi '/home/u45144537/Lauren Brown- Poor Mans Save Folder/dennis.fried/Files for Epi Proj';

/*Keep participants with self-reported type 2 diabetes or no diabetes: N=57,361 observations*/
DATA project_data_1;
SET epi.samadult_combined; /*N=93,442*/
IF DIBTYPE=2 OR DIBEV1=2;
run;

/*Checking code above*/
proc freq data=epi.samadult_combined;
tables DIBEV1 DIBTYPE;
run;

/*Create a new dataset to define outcome and predictors*/
DATA epi.project_data_2;
SET project_data_1;

/*Outcome: Type 2 diabetes*/
IF DIBTYPE=2 then typ2diab=1;  /*Yes*/
else typ2diab=0;               /*No*/

/*Exposure: hypertension*/
if HYPEV=1 then hypertension=1;   /*Yes*/
else if HYPEV=2 then hypertension=0;  /*No*/
else hypertension=.;                 /*Missing*/

/*Other predictors*/
/*Age*/
if age_p>=18 AND age_p<=44 then age_group=1;	
else if age_p>44 AND age_p<=64 then age_group=2;
else if age_p>64 then age_group=3;
else age_group=.;

/*Sex*/
if sex=1 then sex_mod=1;    /*Males*/
else if sex=2 then sex_mod=0; /*Females*/
else sex_mod=.;               /*Missing*/

/*Race*/
if MRACBPI2 IN (1) then race=1; /*White*/
else if MRACBPI2 IN (2) then race=2; /*Black or African American*/
else if MRACBPI2 IN (3) then race=3; /*American Indian*/
else if MRACBPI2 IN (6,7,12) then race=4; /*Asian: Asian Indian, Chinese, Filipino*/
else race=5;                              /*Other or Multiple*/
                          
/*Marital status*/
if R_Maritl IN (4,5,6) then marital_status=3;   /*Formerly married*/
else if R_Maritl IN (1,2,3,8) then marital_status=2; /*Currently married or living with partner*/
else if R_Maritl=7 then marital_status=1;             /*Never married*/
else if R_Maritl=9 then marital_status=.;             

/*Employment status*/
if DOINGLWA IN (1,2,4) then employment=1;        /*Employed*/
else if DOINGLWA IN (3,5) then employment=0;     /*Unemployed*/
else employment=.;                                                       /*Missing*/    

/*Seen by the doctor in the past 12 months*/
if AHCSYR9=1 then doctor=1;                /*Yes*/
else if AHCSYR9=2 then doctor=0;           /*No*/
else if AHCSYR9 not in (1,2) and AHCSYR9 NE . then doctor=2;  /*Unknown*/
else doctor=.;                                                /*Missing*/

/*Has a usual place of care*/
if AUSUALPL IN (1,3) then usual_care=1;   /*Yes*/
else if AUSUALPL=2 then usual_care=0;     /*No*/
else usual_care=.;


/*BMI*/
IF BMI NE . AND BMI NOT IN (11.11, 11.13) AND BMI<18.5 then BMI_cat=1;   /*Underweight*/
else if BMI>=18.5 AND BMI<25 then BMI_cat=2; /*Normal*/
else if BMI>=25 AND BMI<30 then BMI_cat=3;  /*Overweight*/
else if BMI NOT IN (99.99) AND BMI>=30 then BMI_cat=4;             /*Obese*/
else BMI_cat=.;                                                    /*Missing*/

/*Smoking history*/
IF SMKSTAT2=4 then smoking=1; /*Never smoker*/
else if SMKSTAT2=3 then smoking=2; /*Former smoker*/
else if SMKSTAT2 IN (1,2) then smoking=3; /*Current smoker*/
else smoking= .;                                                /*Missing*/

run;

proc format;
value diabetesfm
0='No'
1='Yes';

value hypfm
0='No hypertension'
1='Hypertension';

value agefm
1='18 to 44'
2='45 to 64'
3='65 and above';

value sexfm
0='Females'
1='Males';

value racefm
1='White'
2='Black/African American'
3='American Indian'
4='Asian'
5='Other/Multiple';

value maritalfm
1='Never married'
2='Currently married or living with partner'
3='Formerly married';

value employfm
0='Unemployed'
1='Employed';

value doctorfm
0='Not seen a doctor in the past 12 months'
1='Seen a doctor in the past 12 months'
2='Unknown';

value usualcarefm
0='No usual place of care'
1='Has a usual place of care';

value bmifm
1='Underweight'
2='Normal'
3='Overweight'
4='Obese';

value smokefm
1='Never smoker'
2='Former smoker'
3='Current smoker';

run;

/*Homework 4: Frequency distribution of all variables*/
proc freq data=epi.project_data_2;
tables typ2diab hypertension age_group sex_mod race marital_status employment doctor
usual_care BMI_cat smoking;
format typ2diab diabetesfm. hypertension hypfm. age_group agefm. sex_mod sexfm. race racefm. marital_status maritalfm. employment employfm.
doctor doctorfm. usual_care usualcarefm. BMI_cat bmifm. smoking smokefm.;
run;

/*Homework 4: 10% confounder adjustment rule*/
/*Model with hypertension only*/
proc logistic data=epi.project_data_2;
class hypertension (ref='No hypertension')/ param=ref;
model typ2diab (event='Yes')= hypertension;
format typ2diab diabetesfm. hypertension hypfm.;
run;

/*Hypertension and age*/
proc logistic data=epi.project_data_2;
class hypertension (ref='No hypertension')
age_group (ref='18 to 44')/ param=ref;
model typ2diab (event='Yes')= hypertension age_group;
format typ2diab diabetesfm. hypertension hypfm. age_group agefm.;
run;

/*Hypertension and sex*/
proc logistic data=epi.project_data_2;
class hypertension (ref='No hypertension')
sex_mod (ref='Females')/ param=ref;
model typ2diab (event='Yes')= hypertension sex_mod;
format typ2diab diabetesfm. hypertension hypfm. sex_mod sexfm.;
run;

/*Hypertension and race*/
proc logistic data=epi.project_data_2;
class hypertension (ref='No hypertension')
race (ref='White')/ param=ref;
model typ2diab (event='Yes')= hypertension race;
format typ2diab diabetesfm. hypertension hypfm. race racefm.;
run;

/*Hypertension and marital status*/
proc logistic data=epi.project_data_2;
class hypertension (ref='No hypertension')
marital_status (ref='Never married')/ param=ref;
model typ2diab (event='Yes')= hypertension marital_status;
format typ2diab diabetesfm. hypertension hypfm. marital_status maritalfm.;
run;

/*Hypertension and employment*/
proc logistic data=epi.project_data_2;
class hypertension (ref='No hypertension')
employment (ref='Unemployed')/ param=ref;
model typ2diab (event='Yes')= hypertension employment;
format typ2diab diabetesfm. hypertension hypfm. employment employfm.;
run;

/*Hypertension and seen by the doctor in the past 12 months*/
proc logistic data=epi.project_data_2;
class hypertension (ref='No hypertension')
doctor (ref='Not seen a doctor in the past 12 months')/ param=ref;
model typ2diab (event='Yes')= hypertension doctor;
format typ2diab diabetesfm. hypertension hypfm. doctor doctorfm.;
run;

/*Hypertension and having a usual place of care*/
proc logistic data=epi.project_data_2;
class hypertension (ref='No hypertension')
usual_care (ref='No usual place of care')/ param=ref;
model typ2diab (event='Yes')= hypertension usual_care;
format typ2diab diabetesfm. hypertension hypfm. usual_care usualcarefm.;
run;

/*Hypertension and smoking*/
proc logistic data=epi.project_data_2;
class hypertension (ref='No hypertension')
smoking (ref='Never smoker')/ param=ref;
model typ2diab (event='Yes')= hypertension smoking;
format typ2diab diabetesfm. hypertension hypfm. smoking smokefm.;
run;

/*Hypertension and BMI*/
proc logistic data=epi.project_data_2;
class hypertension (ref='No hypertension')
BMI_cat (ref='Normal')/ param=ref;
model typ2diab (event='Yes')= hypertension BMI_cat;
format typ2diab diabetesfm. hypertension hypfm. BMI_cat bmifm.;
run;

/*Homework 5: Table 1 (descriptive statistics)*/
proc freq data=epi.project_data_2;
tables typ2diab hypertension age_group sex_mod race marital_status
employment doctor usual_care BMI_cat smoking;
format typ2diab diabetesfm. hypertension hypfm. age_group agefm. sex_mod sexfm. race racefm. marital_status maritalfm. employment employfm.
doctor doctorfm. usual_care usualcarefm. BMI_cat bmifm. smoking smokefm.;
run;

/*Homework 5: Table 2 (comparing descriptive statistics between those with and without type 2 diabetes)*/
proc freq data=epi.project_data_2;
tables (hypertension age_group sex_mod race marital_status
employment doctor usual_care BMI_cat smoking)*typ2diab/chisq;
format typ2diab diabetesfm. hypertension hypfm. age_group agefm. sex_mod sexfm. race racefm. marital_status maritalfm. employment employfm.
doctor doctorfm. usual_care usualcarefm. BMI_cat bmifm. smoking smokefm.;
run;

/*Homework 5: Unadjusted logistic regression model: diabetes and hypertension*/
proc logistic data=epi.project_data_2;
class hypertension (ref='No hypertension')/param=ref;
model typ2diab (event='Yes')= hypertension;
format typ2diab diabetesfm. hypertension hypfm.;
run;

/*Diabetes and age*/
proc logistic data=epi.project_data_2 plots=all;
class age_group (ref='18 to 44')/param=ref;
model typ2diab (event='Yes')= age_group;
format typ2diab diabetesfm. age_group agefm.;
run;

/*Diabetes and sex_mod*/
proc logistic data=epi.project_data_2 plots=all;
class sex_mod (ref='Females')/param=ref;
model typ2diab (event='Yes')= sex_mod;
format typ2diab diabetesfm. sex_mod sexfm.;
run;

/*Diabetes and race*/
proc logistic data=epi.project_data_2 plots=all;
class race (ref='White')/param=ref;
model typ2diab (event='Yes')= race;
format typ2diab diabetesfm. race racefm.;
run;

/*Diabetes and marital status*/
proc logistic data=epi.project_data_2 plots=all;
class marital_status (ref='Never married')/param=ref;
model typ2diab (event='Yes')= marital_status;
format typ2diab diabetesfm. marital_status maritalfm.;
run;

/*Diabetes and employment*/
proc logistic data=epi.project_data_2 plots=all;
class employment (ref='Employed')/param=ref;
model typ2diab (event='Yes')= employment;
format typ2diab diabetesfm. employment employfm.;
run;

/*Diabetes and doctor's visit*/
proc logistic data=epi.project_data_2 plots=all;
class doctor (ref='Not seen a doctor in the past 12 months')/param=ref;
model typ2diab (event='Yes')= doctor;
format typ2diab diabetesfm. doctor doctorfm.;
run;

/*Diabetes and usual place of care*/
proc logistic data=epi.project_data_2 plots=all;
class usual_care (ref='No usual place of care')/param=ref;
model typ2diab (event='Yes')= usual_care;
format typ2diab diabetesfm. usual_care usualcarefm.;
run;

/*Diabetes and bmi*/
proc logistic data=epi.project_data_2 plots=all;
class bmi_cat (ref='Normal')/param=ref;
model typ2diab (event='Yes')= bmi_cat;
format typ2diab diabetesfm. bmi_cat bmifm.;
run;

/*Diabetes and smoking*/
proc logistic data=epi.project_data_2 plots=all;
class smoking (ref='Never smoker')/param=ref;
model typ2diab (event='Yes')= smoking;
format typ2diab diabetesfm. smoking smokefm.;
run;

/*Homework 5: Multivariable logistic regression model*/
proc logistic data=epi.project_data_2 plots=all;
class hypertension (ref='No hypertension')
age_group (ref='18 to 44')
sex_mod (ref='Females')
race (ref='White')
marital_status (ref='Never married')
employment (ref='Employed')
doctor (ref='Not seen a doctor in the past 12 months')
usual_care (ref='No usual place of care')
smoking (ref='Never smoker')
BMI_cat (ref='Normal')/param=ref;
model typ2diab (event='Yes')= hypertension age_group sex_mod race marital_status
employment doctor usual_care smoking BMI_cat;
format typ2diab diabetesfm. hypertension hypfm. age_group agefm. sex_mod sexfm. race racefm. marital_status maritalfm.
employment employfm. doctor doctorfm. usual_care usualcarefm. smoking smokefm. BMI_cat bmifm.;
run;
